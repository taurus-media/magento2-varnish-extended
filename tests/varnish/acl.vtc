varnishtest "Don't perform forced cache refresh for non-whitelisted IP addresses"

barrier b1 cond 2

server s1 {
    # first request will be the probe, handle it and be on our way
    rxreq
    expect req.url == "/health_check.php"
    txresp
    barrier b1 sync
} -start

# Generate the VCL file based on included variables and write it to output.vcl
shell {
    export SERVER1_IP="10.0.0.1"
    export s1_addr="${s1_addr}"
    export s1_port="${s1_port}"
    ${testdir}/helpers/parse_vcl.pl "${testdir}/../../etc/varnish6.vcl" "${tmpdir}/output.vcl"
}

varnish v1 -arg "-f" -arg "${tmpdir}/output.vcl" -arg "-p" -arg "vsl_mask=+Hash" -start

# make sure the probe request fired
barrier b1 sync

client c1 {
    # test URL-based cache purge
    txreq -method "PURGE" -url "/"
    rxresp
    expect resp.status == 405

    # test tag-based invalidation
    txreq -method "PURGE" -url "/"  -hdr "X-Magento-Tags-Pattern: ((^|,)cat_p_694(,|$))"
    rxresp
    expect resp.status == 405

    # test full cache purge
    txreq -method "PURGE" -url "/"  -hdr "X-Magento-Tags-Pattern: .*"
    rxresp
    expect resp.status == 405
} -run