varnishtest "Check if Set-Cookie headers are removed for cacheable content"

barrier b1 cond 2

server s1 {
    # first request will be the probe, handle it and be on our way
    rxreq
    expect req.url == "/health_check.php"
    txresp

    # the probe expects the connection to close
    close
    barrier b1 sync
    accept

    rxreq
    expect req.url == "/"
    expect req.method == "GET"
    txresp -hdr "Cache-Control: public, max-age=86400" \
           -hdr "Set-Cookie: testcookie=1; path=/; HttpOnly"
} -start

# Generate the VCL file based on included variables and write it to output.vcl
shell {
    export s1_addr="${s1_addr}"
    export s1_port="${s1_port}"
    ${testdir}/helpers/parse_vcl.pl "${testdir}/../../etc/varnish6.vcl" "${tmpdir}/output.vcl"
}

varnish v1 -arg "-f" -arg "${tmpdir}/output.vcl" -arg "-p" -arg "vsl_mask=+Hash" -start

# make sure the probe request fired
barrier b1 sync

logexpect l1 -v v1 -g request -i BerespUnset -q "ReqUrl eq '/'" {
    expect 0 1002 BerespUnset "Set-Cookie: testcookie=1; path=/; HttpOnly"
} -start

client c1 {
    txreq -method "GET" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"
    expect resp.http.Set-Cookie == <undef>

    txreq -method "GET" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"
    expect resp.http.Set-Cookie == <undef>

    txreq -method "HEAD" -url "/"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"
    expect resp.http.Set-Cookie == <undef>
} -run

logexpect l1 -wait