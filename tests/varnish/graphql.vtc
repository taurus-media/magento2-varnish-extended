varnishtest "GraphQL X-Magento-Cache-Id validation & cache bypassing"

barrier b1 cond 2

server s1 {
    # first request will be the probe, handle it and be on our way
    rxreq
    expect req.url == "/health_check.php"
    txresp

    # the probe expects the connection to close
    close
    barrier b1 sync
    accept

    # 1. First MISS
    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    expect req.http.X-Magento-Cache-Id == <undef>
    txresp

    # 3. UNCACHEABLE (Cache-Id mismatch)
    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    expect req.http.X-Magento-Cache-Id == "1234"
    txresp -hdr "X-Magento-Cache-Id: notmatching"

    # 4. MISS with Cache-Id
    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    expect req.http.X-Magento-Cache-Id == "12345"
    txresp -hdr "X-Magento-Cache-Id: 12345"

    # 6. MISS with different Cache-Id
    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    expect req.http.X-Magento-Cache-Id == "12346"
    txresp -hdr "X-Magento-Cache-Id: 12346"

    # 7. MISS with Store header
    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    expect req.http.X-Magento-Cache-Id == "12345"
    expect req.http.Store == "1"
    txresp -hdr "X-Magento-Cache-Id: 12345" -hdr "Store: 1"

    # 8. MISS with Store and Currency
    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    expect req.http.X-Magento-Cache-Id == "12345"
    expect req.http.Store == "1"
    expect req.http.Content-Currency == "1"
    txresp -hdr "X-Magento-Cache-Id: 12345" -hdr "Store: 1" -hdr "Content-Currency: 1"

    # 9. UNCACHEABLE with Authorization
    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    expect req.http.X-Magento-Cache-Id == <undef>
    expect req.http.Authorization == "Bearer 12345"
    txresp

    # 10. MISS with Authorization and Cache-Id
    rxreq
    expect req.url == "/graphql"
    expect req.method == "GET"
    expect req.http.X-Magento-Cache-Id == "12345"
    expect req.http.Authorization == "Bearer 12345"
    txresp -hdr "X-Magento-Cache-Id: 12345"
} -start

# Generate the VCL file based on included variables and write it to output.vcl
shell {
    export s1_addr="${s1_addr}"
    export s1_port="${s1_port}"
    ${testdir}/helpers/parse_vcl.pl "${testdir}/../../etc/varnish6.vcl" "${tmpdir}/output.vcl"
}

varnish v1 -arg "-f" -arg "${tmpdir}/output.vcl" -arg "-p" -arg "vsl_mask=+Hash" -start

# make sure the probe request fired
barrier b1 sync

client c1 {
    txreq -method "GET" -url "/graphql"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/graphql"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 1234"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "UNCACHEABLE"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 12345"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 12345"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 12346"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 12346"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 12345" \
        -hdr "Store: 1"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 12345" \
        -hdr "Store: 1"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 12345" \
        -hdr "Store: 1" \
        -hdr "Content-Currency: 1"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/graphql" \
        -hdr "X-Magento-Cache-Id: 12345" \
        -hdr "Store: 1" \
        -hdr "Content-Currency: 1"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"

    txreq -method "GET" -url "/graphql" \
        -hdr "Authorization: Bearer 12345"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "UNCACHEABLE"

    txreq -method "GET" -url "/graphql" \
        -hdr "Authorization: Bearer 12345" \
        -hdr "X-Magento-Cache-Id: 12345"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "MISS"

    txreq -method "GET" -url "/graphql" \
        -hdr "Authorization: Bearer 12345" \
        -hdr "X-Magento-Cache-Id: 12345"
    rxresp
    expect resp.http.X-Magento-Cache-Debug == "HIT"
} -run